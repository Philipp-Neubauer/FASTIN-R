model{
  for (j in 1:n.preys){
    
    prop[j] <- ps[j]/sum(ps[1:n.preys] )
    ps[j] ~ dgamma(0.1,1)I(0.01,)
    
    # draw fat content from prior
    fc[j] ~ dnorm(fc_mean[j],fc_tau[j])I(0,)    
    
    # take out of joint posterior for mixing model = make analysis conditional on fc
    #fcc[j] <- cut(fc[j])
    
    # model for sources with Jeffrey's (uninformative, improper) prior
    prey.precs[j,1:m.fats,1:m.fats] ~ dwish(R[1:m.fats,1:m.fats,j],ni[j])
    prey.means[j,1:m.fats] ~  dmnorm(preym[j,],prey.precs[j,,])        
   
    for (f in 1:m.fats){
      # take out of joint posterior for mixing model = make analysis conditional on prey mean distribution
      
      # backtransform to apply fractionation and fat content
      preydd[j,f] <- exp(prey.means[j,f])
      fracs[j,f] ~ dnorm(mean_c[j,f],tau_coeffs[j,f])I(0,)
    }
    fracs[j,n.fats] ~ dnorm(mean_c[j,n.fats],tau_coeffs[j,n.fats])I(0,)
    preydd[j,n.fats] <- 1	    
    preyd[j,1:n.fats] <- preydd[j,1:n.fats]/sum(preydd[j,1:n.fats])    
   
    preyf[j,1:n.fats] <- preyd[j,1:n.fats]*fracs[j,1:n.fats]*fc[j]
      
  }
  
  for(i in 1:n.preds) {      
    
    # predator likelihood
    preds[i,1:m.fats] ~ dmnorm(mu[],predprec[,])
    
  }  
  
  # mixing and alr transformation of predator signature for likelihood
 
    predm[1:n.fats] <- prop[1:n.preys] %*% preyf[1:n.preys,]
 
predf[1:n.fats] <- predm[1:n.fats]/sum(predm[1:n.fats])
  

    mu[1:m.fats] <- log(predf[1:m.fats]/predf[n.fats])
   

                     
  # priors for proportions and predator covariance
   predprec[1:m.fats,1:m.fats] ~ dwish(Rnot[,],m.fats)
 
}
