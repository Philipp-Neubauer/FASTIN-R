model{
  for (j in 1:n.preys){
  
    # draw fat content from prior
    fc[j] ~ dnorm(fc_mean[j],fc_tau[j])I(0,)    
    
    # take out of joint posterior for mixing model = make analysis conditional on fc
    #fcc[j] <- cut(fc[j])
    
    # model for sources with Jeffrey's (uninformative, improper) prior
    prey.precs[j,1:m.fats,1:m.fats] ~ dwish(R[,,j],ni[j])
    prey.means[j,1:m.fats] ~  dmnorm(preym[j,],prey.precs[j,,])        
     
    for (f in 1:m.fats){
      # take out of joint posterior for mixing model = make analysis conditional on prey mean distribution
            
            # backtransform to apply fractionation and fat content
      preydd[j,f] <- exp(prey.means[j,f])
         
    }

    preyd[j,1:m.fats] <- preydd[j,1:m.fats]/(sum(preydd[j,1:m.fats])+1)
    
    preyd[j,n.fats] <- 1/(sum(preydd[j,1:m.fats])+1)
    
    for (f in 1:n.fats){      
      # draw fractionation coeffs for prey j and fatty acid f
      fracs[j,f] ~ dnorm(mean_c[j,f],tau_coeffs[j,f])I(0,)      
      preyf[j,f] <- (preyd[j,f]*fracs[j,f]*fc[j])
      
    }    
  }
  
  for(i in 1:n.preds) {      
    
       # draw clr(proportions)
    pnorm[i,1:n.preys] ~ dmnorm(pmean[],pprec[,])
    for(j in 1:n.preys) {
      
      # backtransform clr proportions by clr^-1
      p_unn[i,j] <- exp(pnorm[i,j])
            
      #closure
     
      
     
    }
    
 prop[i,1:n.preys] <- (p_unn[i,1:n.preys])/sum(p_unn[i,1:n.preys])

      predm[i,1:n.fats] <- prop[i,1:n.preys] %*% preyf[1:n.preys,]
      predf[i,1:n.fats] <- predm[i,]/sum(predm[i,])
   
     mu[i,1:m.fats] <- log(predf[i,1:m.fats]/predf[i,n.fats])
    # predator likelihood
    preds[i,1:m.fats] ~ dmnorm(mu[i,],predprec[,])
    
    # mixing and alr transformation of predator signature for likelihood
        
             
  }  
  
  # priors for proportions and predator covariance
   predprec[1:m.fats,1:m.fats] ~ dwish(Rnot[,],m.fats)
   pmean[1:n.preys] ~ dmnorm(zeros[],S[,])
 
  for(j in 1:n.preys) {  
    pm_unn[j] <- exp(pmean[j])    
    #closure
    pop.prop[j] <- (pm_unn[j])/sum(pm_unn[1:n.preys])
  }
    
  pprec[1:n.preys,1:n.preys] ~ dwish(SS[,],n.preys)
}
