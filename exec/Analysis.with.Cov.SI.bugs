model{
  for (j in 1:n.preys){
  
  # model for sources with Jeffrey's (uninformative, improper) prior
   
    prey.precs_SI[j,1:isos,1:isos] ~ dwish(R_SI[,,j],ni.SI[j])
    prey.means_SI[j,1:isos] ~  dmnorm(preym.SI[j,],prey.precs_SI[j,,])
    
    for (i in 1:isos)
    { cs[j,i] ~ dnorm(mean_cs[j,i],tau_cs[j,i])
      # take out of joint posterior for mixing model = make analysis conditional on prey mean distribution
      prey_SI[j,i] <- cs[j,i]+prey.means_SI[j,i]
      
    }
}
  
# regression coeffs
for (k in 1:n.covs){
  beta[1:n.preys,k] ~ dmnorm(zeros[],S[,])
}

  for(i in 1:n.preds) {      
    
       # draw clr(proportions)
mus[i,1:n.preys] <- beta[1:n.preys,1:n.covs] %*% Covs[i,1:n.covs]
   #pnorm[i,1:n.preys] ~ dmnorm(mus[i,],pprec[,])
    for(j in 1:n.preys) {
      
      # backtransform clr proportions by clr^-1
      p_unn[i,j] <- exp(mus[i,j])
          
    }
    
prop[i,1:n.preys] <- (p_unn[i,1:n.preys])/sum(p_unn[i,1:n.preys])

   # predator likelihood
    
    preds.SI[i,1:isos] ~ dmnorm(mu_SI[i,],predprec_SI[,])
 
  # mixing for predator SI signature for likelihood
 
  mu_SI[i,1:isos] <- prop[i,1:n.preys] %*% prey_SI[1:n.preys,]
    
  
  }   
        

  # priors for proportions and predator covariance
   predprec_SI[1:isos,1:isos] ~ dwish(Rnot_SI[,],isos)
  
  for(j in 1:n.preys) {  
    pm_unn[j] <- exp(beta[j,1])    
    #closure
    pop.prop[j] <- (pm_unn[j])/sum(pm_unn[1:n.preys])
  }
    
  #pprec[1:n.preys,1:n.preys] ~ dwish(SS[,],n.preys)
}
