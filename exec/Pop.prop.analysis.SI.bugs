model{
  for (j in 1:n.preys){
    
    prop[j] <- ps[j]/sum(ps[1:n.preys] )
    ps[j] ~ dgamma(0.1,1)I(0.01,)
    
     
    # model for sources with Jeffrey's (uninformative, improper) prior
      
     prey.precs_SI[j,1:isos,1:isos] ~ dwish(R_SI[,,j],ni.SI[j])
    prey.means_SI[j,1:isos] ~  dmnorm(preym.SI[j,],Rs_SI[j,,])
    
    for (i in 1:isos)
    { cs[j,i] ~ dnorm(mean_cs[j,i],tau_cs[j,i])
      # take out of joint posterior for mixing model = make analysis conditional on prey mean distribution
      prey.mc_SI[j,i] <-cut(prey.means_SI[j,i])
      prey_SI[j,i] <- cs[j,i]+prey.mc_SI[j,i]
      
      for (k in 1:isos)
      {
        Rs_SI[j,i,k] <- prey.precs_SI[j,i,k]*ni.SI[j]
      }
    }
   
    
  }
  
  for(i in 1:n.preds) {     
       
    # predator likelihood
    
    preds.SI[i,1:isos] ~ dmnorm(mu_SI[],predprec_SI[,])
      
    }
 
  # mixing for predator SI signature for likelihood
  for (k in 1:isos){ 
    mu_SI[k] <- inprod(prop[1:n.preys],prey_SI[1:n.preys,k])
    
  }
  
  # priors for proportions and predator covariance
  
   predprec_SI[1:isos,1:isos] ~ dwish(Rnot_SI[,],isos)
  
}
